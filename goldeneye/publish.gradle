apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.kotlin.srcDirs
}

task javadocsJar(type: Jar, dependsOn: "dokkaJavadoc") {
    archiveClassifier.set("javadoc")
    from dokkaJavadoc.outputDirectory
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name sonatype.name()
                url sonatype.url()
                credentials {
                    username sonatype.username()
                    password sonatype.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = 'co.infinum'
                version = versions.goldeneye

                artifactId = 'goldeneye'

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'GoldenEye'
                    description = 'A wrapper for Camera1 and Camera2 API which exposes simple to use interface.'
                    url = 'https://github.com/infinum/Android-GoldenEye'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'novakmarijan'
                            name = 'Marijan Novak'
                            email = 'marijan.novak@infinum.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/infinum/Android-GoldenEye.git'
                        developerConnection = 'https://github.com/infinum/Android-GoldenEye.git'
                        url = 'https://github.com/infinum/Android-GoldenEye'
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}