task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
    javadocs.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task javadocsJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from dokka.outputDirectory
}

task sourcesJar(type: Jar, dependsOn: assemble) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task generateReadme {
    doFirst {
        def readmeFile = new File("${project.rootDir}/README.md")
        def content = readmeFile.text
        content = content.replaceAll("'co\\.infinum:goldeneye:.+?'", "'co.infinum:goldeneye:${versions.goldeneye}'")
        readmeFile.setText(content)
    }
}